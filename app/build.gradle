apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'//butterknife
def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

//获取自动版本号
def gitVersionCode() {
    def cmd = 'git rev-list HEAD --count'
//    cmd.execute().text.trim().toInteger()
    return 1.0;
}


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "net.huaxi.reader"
        minSdkVersion 14
        targetSdkVersion 22
        //最新版本:106;
        versionCode 10000 //此值在release时自动生成。
        //版本名每次正式发包时需要修改。
        versionName "1.0.3"
        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        // manifestPlaceholders = [CHANNEL: "umeng"]
        ndk {
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a', 'mips', 'mips64'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    //该配置为了提升编译速度
    dexOptions {
        incremental true
    }

    productFlavors {
        yingyongbao {}  //默认渠道改为000;
        huaxiweb {}
        huaxi360 {}
        xiaomi {}
        baidu {}
        Alibaba {}
        jifeng {}
        zhushou360 {}
        baiduzhushou {}
        wandoujia {}
        anzhi {}
        yingyonghui {}
        sougoushichang {}
        sougouzhushou {}
        meizu {}
        lenovo {}
        oppo {}
        vivo {}
    }
    //批量配置
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [CHANNEL: name]
    }

    signingConfigs {
        debug {
            keyAlias 'huaxi'
            keyPassword '8daa28f6be2d187b52dc0e93214a58f9'
            storeFile file("../huaxi_keystore2.jks")
            storePassword '8daa28f6be2d187b52dc0e93214a58f9'

        }
        release {
            keyAlias 'huaxi'
            keyPassword '8daa28f6be2d187b52dc0e93214a58f9'
            storeFile file("../huaxi_keystore2.jks")
            storePassword '8daa28f6be2d187b52dc0e93214a58f9'
        }
    }
    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "debuggable", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "debuggable", "false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources true
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release
        }

        //定义输出apk的名字
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                if (variant.buildType.name.equals('release')) {
                    variant.mergedFlavor.versionCode = gitVersionCode();
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
//                         输出apk名称为boohee_release_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "huaxi_release_v${defaultConfig.versionName}.${variant.mergedFlavor.versionCode}_${releaseTime()}${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                } else if (variant.buildType.name.equals('debug')) {
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为boohee_release_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "huaxi_debug_v${defaultConfig.versionName}.${variant.mergedFlavor.versionCode}_${releaseTime()}${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    sourceSets.main {
        jniLibs.srcDir 'libs'
    }
}

//对话框库的必要添加
allprojects {
    repositories {
        //阿里百川添加
        flatDir {
            dirs 'libs'
        }
        jcenter()
        maven { url "https://jitpack.io" }
    }
}
//butterknife配置
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':xs-libs')
    //    releaseCompile project(path: ':xs-libs', configuration: 'release')
    //    debugCompile project(path: ':xs-libs', configuration: 'debug')
    compile project(':volley')
    //主题色选择
    //    compile 'com.android.support:palette-v7:23.+'
    //    compile 'com.android.support:appcomlipaySDpat-v7:23.1.1'
    //对话框的核心库，github地址：https://github.com/afollestad/material-dialogs
    compile('com.github.afollestad.material-dialogs:core:0.8.5.8@aar') {
        transitive = true
    }
    //阿里百川添加
    compile(name: 'feedbackSdk', ext: 'aar')
    //    搜索的提示的listview，https://github.com/emilsjolander/StickyListHeaders
    //    https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    //    https://github.com/JakeWharton/butterknife
    //https://github.com/robinhood/ticker    //增币对话框的中滚动的textview
    //修改activity状态栏的颜色
    //https://github.com/yqritc/RecyclerView-FlexibleDivider //recyclerview的分割线
    //compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'  代码已经考入divider包中
    compile 'com.android.support:support-v4:23.1.1'
    compile 'in.srain.cube:ultra-ptr:1.0.11'
    compile 'com.jakewharton.hugo:hugo-runtime:1.2.1'
    compile 'com.github.chenupt.android:springindicator:1.0.2@aar'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.ogaclejapan.smarttablayout:library:1.6.0@aar'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:v1.9.3'
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'cn.lankton:flowlayout:2.0.0'
    compile 'com.robinhood.ticker:ticker:1.0.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.githang:status-bar-compat:0.3'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}


